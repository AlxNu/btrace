//
// This file is to be applied to every subproject.
//

apply plugin: 'java'
apply plugin: 'idea'

project.group = 'org.openjdk.btrace'
project.version = '2.3.0-SNAPSHOT'

buildscript {
    repositories {
        mavenCentral()
    }
}

project.ext.setProperty('compiler8', javaToolchains.compilerFor {
    languageVersion.set(JavaLanguageVersion.of(8))
    vendor.set(JvmVendorSpec.ADOPTIUM)
})
project.ext.setProperty('home8', compiler8.map { it.metadata.installationPath })
project.ext.setProperty('javadoctool8', javaToolchains.javadocToolFor {
    languageVersion.set(JavaLanguageVersion.of(8))
    vendor.set(JvmVendorSpec.ADOPTIUM)
})
project.ext.setProperty('launcher8', javaToolchains.launcherFor {
    languageVersion.set(JavaLanguageVersion.of(8))
    vendor.set(JvmVendorSpec.ADOPTIUM)
})
project.ext.setProperty('java8', launcher8.map { it.executablePath })

project.ext.setProperty('compiler11', javaToolchains.compilerFor {
    languageVersion.set(JavaLanguageVersion.of(11))
    vendor.set(JvmVendorSpec.ADOPTIUM)
})
project.ext.setProperty('home11', compiler11.map { it.metadata.installationPath })

project.ext.setProperty('compiler17', javaToolchains.compilerFor {
    languageVersion.set(JavaLanguageVersion.of(17))
    vendor.set(JvmVendorSpec.ADOPTIUM)
})
project.ext.setProperty('home17', compiler17.map { it.metadata.installationPath })

compileJava {
    javaCompiler = compiler8
}

compileTestJava {
    javaCompiler = compiler8
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
    // You may define additional repositories, or even remove "mavenCentral()".
    // Read more about repositories here:
    //   http://www.gradle.org/docs/current/userguide/dependency_management.html#sec:repositories
}

dependencies {
    // Adding dependencies here will add the dependencies to each subproject.
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
}

String mavenArtifactId = name

javadoc {
    javadocTool = javadoctool8
    options.addStringOption('Xdoclint:all,-missing', '-quiet')
    failOnError true
}

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

task createFolders(description: 'Creates the source folders if they do not exist.') doLast {
    sourceSets*.allSource*.srcDirs*.each { File srcDir ->
        if (!srcDir.isDirectory()) {
            println "Creating source folder: ${srcDir}"
            srcDir.mkdirs()
        }
    }
}

test {
    useJUnitPlatform()
    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        events "passed", "skipped", "failed"
    }
}